// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // Determine whether any chip should carry LOAD.
    // (a OR b) represent the RAM16K range, c represents the Screen (8K) range, and d is discarded and not used.
    DMux4Way(in=load, sel=address[13..14], a=a, b=b, c=c, d=d);
    Or(a=a, b=b, out=load16K);

    RAM16K(in=in, load=load16K, address=address[0..13], out=ram16K);
    Screen(in=in, load=c, address=address[0..12], out=screen);
    Keyboard(out=keyboard);

    // naive way of doing this; everything above 0xFFFF is keyboard.
    Mux4Way16(a=ram16K, b=ram16K, c=screen, d=keyboard, sel=address[13..14], out=out);
}