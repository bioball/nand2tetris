// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:

    // Perform add 1
    Add16 (a=iOut, b[0]=true, out=inPlusOne);
    // If inc is 1, add one
    Mux16 (a=iOut, b=inPlusOne, sel=inc, out=inPlusOneIfInc);
    // Else if load is 1, output in
    Mux16 (a=inPlusOneIfInc, b=in, sel=load, out=loadIfLoad);
    // Else if reset is 1, output 0
    Mux16 (a=loadIfLoad, sel=reset, out=zeroIfReset);
    // Pipe to a register
    Register (in=zeroIfReset, load=true, out=iOut, out=out);

}
